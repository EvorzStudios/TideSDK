#!/usr/bin/env python

# 
# Copyright (c) 2012 - 2014 TideSDK contributors 
# http://www.tidesdk.org
# Includes modified sources under the Apache 2 License
# Copyright (c) 2008 - 2012 Appcelerator Inc
# Refer to LICENSE for details of distribution and use.
# 


import os, os.path as p, distutils.dir_util as dir_util

Import('build','LIBUTILS_NAME')

Env = build.env.Clone()

Env.Append(LIBS=[LIBUTILS_NAME])
Env.Append(LIBPATH=[os.path.join(build.dir, 'objs')])
Env.Append(FRAMEWORKS=['Cocoa'])
sources = Glob('*.m') + Glob('*.mm')

# Optimize the installer for size.
ccflags = list(Env['CCFLAGS'])
ccflags.append('-Oz')
Env['CCFLAGS'] = ccflags

# We want to build this directly into the runtime dist directory to avoid copying
# In the end we want something that looks like this inside of build.runtime_build_dir
#    installer/Installer App.app/Contents/MacOS/Installer App
#    installer/Installer App.app/Contents/Resources/English.lproj/MainMenu.nib
#    installer/Installer App.app/Contents/Resources/English.lproj/tidesdk.icns
#    installer/Installer App.app/Contents/Info.plist
#    see installation/distribution_guide.txt
contents = p.join(build.dir, 'sdk', 'installer', 'Installer App.app', 'Contents')
inst = Env.Program(p.join(contents, 'MacOS', 'Installer App'), sources)
Env.AddPostAction(inst, 'strip -u -r "%s"' % str(inst[0]))
build.mark_build_target(inst)

lproj_dir = p.join(contents, 'Resources', 'English.lproj')
build.mark_build_target(build.utils.CopyToDir(Glob('*.plist'), contents))
build.mark_build_target(build.utils.CopyToDir([
    p.join(build.tide_sdk_dir, 'osx', 'tidesdk.icns'),
    p.join(build.cwd(), 'dialog-warning.png')], lproj_dir))

for f in Glob('English.lproj/*.xib'):
    f = str(f)
    out = p.join(lproj_dir, p.splitext(p.basename(f))[0] + '.nib')
    Env.AddPostAction(inst, 'ibtool --errors --warnings --notices --output-format human-readable-text --compile "%s" "%s"' % (out, f))
