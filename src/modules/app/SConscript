#!/usr/bin/env python

# 
# Copyright (c) 2012 - 2014 TideSDK contributors 
# http://www.tidesdk.org
# Includes modified sources under the Apache 2 License
# Copyright (c) 2008 - 2012 Appcelerator Inc
# Refer to LICENSE for details of distribution and use.
# 

from os import path
import os
Import('build env module')

env.Append(CPPDEFINES = ('TIDESDK_APP_API_EXPORT', 1))

build.add_thirdparty(env, 'poco')
build.add_thirdparty(env, 'libxml')

if build.is_win32():
	env.Append(CCFLAGS=['/MD', '/DUNICODE', '/D_UNICODE'])
	build.add_thirdparty(env, 'webkit')
else:
	env.Append(ENV = {'PATH' : os.environ['PATH']})
	if os.environ.has_key('PKG_CONFIG_PATH'):
		env.AppendENVPath('PKG_CONFIG_PATH', os.environ['PKG_CONFIG_PATH'])
	if not build.is_osx():
		env.ParseConfig('pkg-config --cflags --libs libxml-2.0')

sources = Glob('*.cpp')
if build.is_win32():
	sources += Glob('win32/*.cpp')
elif build.is_linux():
	sources += Glob('linux/*.cpp')
elif build.is_osx():
	sources += Glob('osx/*.mm')

build.mark_build_target(env.SharedLibrary(path.join(module.dir, 'tideapp'), sources))
build.mark_build_target(build.utils.CopyTree(Glob('*.js'), module.dir))
