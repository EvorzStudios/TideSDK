#!/usr/bin/env python

# 
# Copyright (c) 2012 - 2014 TideSDK contributors 
# http://www.tidesdk.org
# Includes modified sources under the Apache 2 License
# Copyright (c) 2008 - 2012 Appcelerator Inc
# Refer to LICENSE for details of distribution and use.
# 

from os import path
Import('build env module')

env.Append(LIBPATH=[build.get_module('app').dir])
env.Append(CPPPATH = ['%s/src/modules/app' % build.tide_source_dir])

build.add_thirdparty(env, 'poco')
build.add_thirdparty(env, 'webkit')
	
env.Append(LIBS=['tideapp'])

sources = Glob('*.cpp') + ['url/url.cpp']

if not build.tidelite:
    sources = sources + Glob('lang_binding/*.cpp')

if build.is_linux():
    sources = sources + Glob('gtk/*.cpp')
    build.add_thirdparty(env, 'libsoup')
    env.Append(LIBS=['Xss'])
    env.ParseConfig('PKG_CONFIG_PATH=${PKG_CONFIG_PATH} pkg-config --cflags --libs libnotify gtk+-2.0 gdk-2.0 x11 xext')

elif build.is_win32():
    build.add_thirdparty(env, 'cairo')
    build.add_thirdparty(env, 'curl')
    sources = sources + Glob('win32/*.cpp')
    sources += Glob('binding/kjs*.cpp')
    env.Append(CPPPATH=[build.tp('icu', 'include')])
    env.Append(LIBS=[
        'kernel32', 'shell32', 'user32', 'ole32', 'comdlg32', 'comctl32',
        'gdi32', 'shlwapi', 'oleaut32', 'icuuc', 'pthreadVC2', 'libcurl_imp',
        'comsuppw', 'advapi32'])
    env.Append(CCFLAGS=['/MD', '/DUNICODE', '/D_UNICODE'])
    env.Append(LINKFLAGS=['/NODEFAULTLIB:LIBCMT.lib', '/LTCG', '/INCREMENTAL:NO', '/OPT:REF'])

elif build.is_osx():
    env.Append(FRAMEWORKPATH=[build.tp('growl')])
    env.Append(FRAMEWORKS=['Cocoa','Carbon', 'IOKit', 'Growl'])
    sources = sources + Glob('osx/*.mm') + Glob('osx/*.cpp')
    env.Append(CCFLAGS=['-x', 'objective-c++'])

# Copy the support files the module build directory
build.utils.LightWeightCopy(path.join(build.cwd(), 'support'), module.dir)
build.mark_build_target(env.SharedLibrary(
    path.join(module.dir,'tideui'), sources))
